# import yaml
from src.aggregate_pm25 import available_shapefile_year
from hydra import compose, initialize

conda: "requirements.yaml"
configfile: "conf/snakemake.yaml"

# defaults_dict = {key: value for d in config['defaults'] if isinstance(d, dict) for key, value in d.items()}

# polygon_name=config["polygon_name"]
# temporal_freq = config['temporal_freq']

# shapefiles_cfg = yaml.safe_load(open(f"conf/shapefiles/shapefiles.yaml", 'r'))
# satellite_pm25_cfg = yaml.safe_load(open(f"conf/satellite_pm25/us_pm25.yaml", 'r'))

# shapefile_years_list = list(shapefiles_cfg[polygon_name].keys())

temporal_freq = config['temporal_freq']
polygon_name = config['polygon_name']
components = config['components']

# resolution in degrees for the dataloader
resolution = config['dataloader_resolution']
resolution_str = str(resolution).replace(".", "_")

with initialize(version_base=None, config_path="conf"):
    hydra_cfg = compose(config_name="config", overrides=[f"temporal_freq={temporal_freq}", f"polygon_name={polygon_name}"])

satellite_pm25_cfg = hydra_cfg.satellite_pm25
shapefiles_cfg = hydra_cfg.shapefiles

months_list = "01" if temporal_freq == 'annual' else [str(i).zfill(2) for i in range(1, 12 + 1)]
years_list = list(range(1998, 2022 + 1))

# == Define rules ==
rule all:
    input:
        expand(f"data/output/satellite_pm25_raster2polygon/{temporal_freq}/satellite_pm25_{polygon_name}_" + 
                ("{year}.parquet" if temporal_freq == 'annual' else "{year}_{month}.parquet"), 
            year=years_list,
            month=months_list
        )

rule download_shapefiles:
    output:
        f"data/input/shapefiles/shapefile_{polygon_name}_" + "{shapefile_year}/shapefile.shp" 
    shell:
        f"python src/download_shapefile.py polygon_name={polygon_name} " + "shapefile_year={wildcards.shapefile_year}"

rule download_satellite_pm25:
    output:
        expand(
            f"data/input/satellite_pm25/{temporal_freq}/{satellite_pm25_cfg[temporal_freq]['file_prefix']}." + 
            ("{year}01-{year}12.nc" if temporal_freq == 'annual' else "{year}{month}-{year}{month}.nc"), 
            year=years_list,
            month=months_list)
    log:    
        f"logs/download_satellite_pm25_{temporal_freq}.log"
    shell:
        f"python src/download_pm25.py temporal_freq={temporal_freq} " + " &> {log}"


rule download_all_components:
    input:
        expand(
            f"data/input/pm25_components__washu__raw/{temporal_freq}/{{component}}/",
            component=components
        )


rule download_component:
    output:
        directory(f"data/input/pm25_components__washu__raw/{config['temporal_freq']}/{{component}}/")
    log:    
        "logs/download_components_{component}.log"
    shell:
        f"python src/download_components.py temporal_freq={config['temporal_freq']} component={{wildcards.component}} &> {{log}}"


rule preprocess_all_components_dataloader:
    input:
        expand(
            f"data/input/pm25_components__washu__grid_{resolution_str}__dataloader/{temporal_freq}/{{component}}/",
            component=components
        )


rule preprocess_component_dataloader:
    input:
        f"data/input/pm25_components__washu__raw/{temporal_freq}/{{component}}/" 
    output:
        directory(f"data/input/pm25_components__washu__grid_{resolution_str}__dataloader/{temporal_freq}/{{component}}/")
    log:
        "logs/preprocess_component_{component}.log"
    shell:
        f"python src/preprocess_grids.py temporal_freq={temporal_freq}"
        f" component={{wildcards.component}} resolution={resolution} &> {{log}}"


rule dataloader:
    input:
        expand(
            f"data/input/pm25_components__washu__grid_{resolution_str}__dataloader/{temporal_freq}/{{component}}/",
            component=components
        )
    output:
       f"data/input/pm25_components__washu__grid_{resolution_str}__dataloader/{temporal_freq}/summary.json"
    log:
        "logs/dataloader.log"
    shell:
        f"python src/dataloader.py temporal_freq={temporal_freq} resolution={resolution} &> {{log}}"



def get_shapefile_input(wildcards):
    shapefile_year = available_shapefile_year(int(wildcards.year), shapefile_years_list)
    return f"data/input/shapefiles/shapefile_{polygon_name}_{shapefile_year}/shapefile.shp"


rule aggregate_pm25:
    input:
        get_shapefile_input,
        expand(
            f"data/input/satellite_pm25/{temporal_freq}/{satellite_pm25_cfg[temporal_freq]['file_prefix']}." + 
            ("{{year}}01-{{year}}12.nc" if temporal_freq == 'annual' else "{{year}}{month}-{{year}}{month}.nc"), 
            month=months_list
        )

    output:
        expand(
            f"data/output/satellite_pm25_raster2polygon/{temporal_freq}/satellite_pm25_{polygon_name}_" + 
            ("{{year}}.parquet" if temporal_freq == 'annual' else "{{year}}_{month}.parquet"), 
            month=months_list  # we only want to expand months_list and keep year as wildcard
        )
    log:
        f"logs/satellite_pm25_{polygon_name}_{{year}}.log"
    shell:
        (
            f"PYTHONPATH=. python src/aggregate_pm25.py polygon_name={polygon_name} temporal_freq={temporal_freq} " + 
            ("year={wildcards.year}" if temporal_freq == 'annual' else "year={wildcards.year}") +
            " &> {log}"
        )
